import from mtllm.llms { OpenAI }

glob llm = OpenAI(verbose=False, model_name="gpt-4o-mini");

enum Personality {
    INTROVERT = "Introvert",
    EXTROVERT = "Extrovert"
}

obj Person {
    has full_name: str,
        yod: int,
        personality: Personality;
}

glob personality_examples: dict[str, Personality] = {
    'Albert Einstein': Personality.INTROVERT,
    'Barack Obama': Personality.EXTROVERT
};

def get_person_info(name: str) -> Person by llm(
    reason=True,
    temperature=0.0,
    incl_info=(personality_examples)
);

with entry {
    person_obj = get_person_info('Martin Luther King Jr.');
    print(
        f"{person_obj.full_name} was a {person_obj.personality.value} person who died in {person_obj.yod}"
    );
}
