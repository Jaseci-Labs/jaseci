---
sidebar_position: 4
---

import Tabs from '@theme/Tabs';
import TabItem from '@theme/TabItem';

# Full demo code

## Jac code

Here's the full Jac code for the social network. This is what we'll be referencing and using in the various examples.

`social.jac`

```
// person node to represent members of the network
node person {
    has anchor name;
}

// friend edge to describe the relationship between person nodes
edge friend;

walker join {
    has name;

    root {
        report spawn here <--> node::person(name=name);
    }
}

// gets all members of the network
walker get_members {
    has anchor members;

    with entry {
        members = [];
    }

    root {
        take -->;
    }

    person {
        members += [here];
    }
}

// adds a friend to the person node being walked
walker add_friend {
    has new_friend;

    person {
        here <-[friend]-> new_friend;
    }
}

// get the list of friends of the person node being walked
walker get_friend_list {
    has anchor friend_list;

    with entry {
        friend_list = [];
    }

    person {
        friend_list += <-[friend]->;
    }

    with exit {
        for friend in friend_list {
            report friend;
        }
    }
}

// gets suggested friends for the person node being walked
// current algorithm: suggest friends of friends (mutual friends)
walker get_suggested_friends {
    has anchor suggestions;

    with entry {
        suggestions = [];
    }

    person {
        for f in <-[friend]-> {
            for sug in spawn f walker::get_friend_list() {
                if sug != here {
                    suggestions += [sug];
                    report sug;
                }
            }
        }
    }
}
```

## Demo logic

This is the full version of the demo logic that makes use of the structures define in demo.jac. It acts as a frontend that interacts with your Jac code on the Jaseci engine.

<Tabs>
  <TabItem value="python-requests" label="Python (requests)" default>
   

        import requests

        # API URLs
        url_user_create = "http://<hostname>/user/create/"
        url_user_token = "http://<hostname>/user/token/"
        url_create_graph = "http://<hostname>/jac/create_graph"
        url_create_sentinel = "http://<hostname>/jac/create_sentinel"
        url_set_jac = "http://<hostname>/jac/set_jac"
        url_run = "http://<hostname>/jac/run"

        # create user
        payload = {
        "name": "Test User",
        "email": "test@example.com",
        "password": "testpassword",
        "is_activated": True
        }
        response = requests.post(url_user_create, json=payload)

        # get auth token
        payload = {
        "email": "test@example.com",
        "password": "testpassword"
        }
        response = requests.post(url_user_token, json=payload)
        auth = response.json()
        auth_token = auth['token']

        headers = {
        'Authorization': f'Token {auth_token}'
        }

        # create graph
        payload = {
        "name": "Test graph"
        }
        response = requests.post(url_create_graph, headers=headers, json=payload)
        graph = response.json()
        graph_id = graph['jid']

        # create sentinel
        payload = {
        "name": "Test sentinel"
        }
        response = requests.post(url_create_sentinel, headers=headers, json=payload)
        sentinel = response.json()
        sentinel_id = sentinel['jid']

        # set jac code
        with open("social.jac") as f:
            code = f.read()

        payload = {
        "snt": sentinel_id,
        "code": code,
        "encoded": False
        }
        response = requests.post(url_set_jac, headers=headers, json=payload)

        # using the network

        # create members
        member_nodes = []
        names = ['Jake', 'Amy', 'Charles', 'Rosa', 'Gina']
        print("CREATING MEMBERS")
        for name in names:
            payload = {
                "name": "join",
                "snt": sentinel_id,
                "nd": graph_id,  # we use the graph id here to run the walker on the root node
                "ctx": {
                    'name': name
                }
            }
            response = requests.post(url_run, headers=headers, json=payload)
            walker_result = response.json()
            member_node = walker_result[0]
            member_nodes.append(member_node)
            print(f' - {member_node["context"]["name"]}')

        # add friends
        print('ADDING FRIENDS')
        for i, member in enumerate(member_nodes):
            new_friend = member_nodes[(i + 1) % len(member_nodes)]
            payload = {
                "name": "add_friend",
                "snt": sentinel_id,
                'nd': member['jid'],  # run on person node we want to add friend to
                'ctx': {
                    'new_friend': new_friend['jid']
                }
            }
            response = requests.post(url_run, headers=headers, json=payload)

        # get and display friends
        for member in member_nodes:
            payload = {
                "name": "get_friend_list",
                "snt": sentinel_id,
                'nd': member['jid'],  # run on person node we want the friend list for
            }
            response = requests.post(url_run, headers=headers, json=payload)
            friend_list = response.json()

            print(f'{member["context"]["name"]}\'s friends:')
            for friend in friend_list:
                print(f' - {friend["context"]["name"]}')

        # get suggested friends
        print('GETTING SUGGESTED FRIENDS')
        for member in member_nodes:
            payload = {
                "name": "get_suggested_friends",
                "snt": sentinel_id,
                "nd": member['jid'],  # run on person node we want the suggested friends for
            }
            response = requests.post(url_run, headers=headers, json=payload)
            suggested = response.json()
            print(f'Suggestions for: {member["context"]["name"]}')
            for sug in suggested:
                print(f' - {sug["context"]["name"]}')
            print()
    
  </TabItem>

  <TabItem value="jac" label="Jac">
  <p>NOTE: This code is shown just for comparison and requires additional steps to load it into Jaseci and run the init walker.</p>

        // <include contents of social.jac here>

        walker demo_create_members {
            has members;

            with entry {
                std.out("CREATING MEMBERS");
                members = ["Jake", "Amy", "Charles", "Rosa", "Gina"];
                for name in members {
                    spawn here walker::join(name=name);
                }
            }

            root {
                take -->;
            }

            person {
                std.out(" - " + here.name);
            }
        }

        walker demo_add_friends {
            has members;

            with entry {
                std.out("ADDING FRIENDS");
                members = spawn here walker::get_members;

                for i=0 to i<members.length by i+=1 {
                    member = members[i];
                    new_friend = members[(i+1) % members.length];
                    spawn member walker::add_friend(new_friend=new_friend);
                }
            }

            root {
                take -->;
            }

            person {
                std.out(here.name + "'s friends: ");
                for f in <-[friend]-> {
                    std.out(" - " + f.name);
                }
                std.out("");
            }
        }

        walker demo_get_suggested_friends {
            with entry {
                std.out("GETTING SUGGESTED FRIENDS");
                members = spawn here walker::get_members;

                for member in members {
                    std.out(member.name + "'s suggestions");
                    for suggested in spawn member walker::get_suggested_friends {
                        std.out(" - " + suggested.name);
                    }
                    std.out("");
                }
            }
        }

        // runs all demo walkers
        // this is the entry point of the program and is configured to be run by the demo code
        walker init {
            spawn here walker::demo_create_members;
            std.out("\n");
            spawn here walker::demo_add_friends;
            std.out("\n");
            spawn here walker::demo_get_suggested_friends;
        }


  </TabItem>

</Tabs>

OUTPUT

```
CREATING MEMBERS
 - Jake
 - Amy
 - Charles
 - Rosa
 - Gina

ADDING FRIENDS
Jake's friends: 
 - Amy
 - Gina

Amy's friends: 
 - Charles
 - Jake

Charles's friends: 
 - Rosa
 - Amy

Rosa's friends: 
 - Gina
 - Charles

Gina's friends: 
 - Jake
 - Rosa

GETTING SUGGESTED FRIENDS
Jake's suggestions
 - Charles
 - Rosa

Amy's suggestions
 - Rosa
 - Gina

Charles's suggestions
 - Gina
 - Jake

Rosa's suggestions
 - Jake
 - Amy

Gina's suggestions
 - Amy
 - Charles
```

You've reached the end of the demo, now proceed to the [next section](/docs/your-first-jac-program/next-steps) for next steps.